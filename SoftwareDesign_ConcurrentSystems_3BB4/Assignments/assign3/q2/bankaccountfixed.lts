const MaxInt = 1
range Nat    = 0 .. MaxInt

LOCK = ( acquire ->
         release ->
         LOCK ).

BALANCE = BALANCE[ 0 ],

    BALANCE[ current: Nat ] = ( increase[ amount: Nat ] ->
                                
                                if
                                    current+amount <= MaxInt
                                then
                                    BALANCE[ current+amount ]
                                else
                                    ( overflow.exception ->
                                      BALANCE[ current ] )
                              
                              | decrease[ amount: Nat ] ->
                                
                                if
                                    current-amount >= 0
                                then
                                    BALANCE[ current-amount ]
                                else
                                    ( insufficient.funds.exception ->
                                      BALANCE[ current ] ))
\{
    overflow.exception
}.

WITHDRAW = ( call ->
             total[ amount: Nat ] ->
             WITHDRAW[ amount ] ),

    WITHDRAW[ amount: Nat ] = (  lock.acquire ->
                                 balance.decrease[ amount ] ->
                                 
                                 ( lock.release ->
                                   return ->
                                   WITHDRAW
             
                                 | balance.insufficient.funds.exception ->
                                   lock.release ->
                                   return ->
                                   WITHDRAW ))
+{
    balance.increase[ amount: Nat ]
}.

DEPOSIT = ( call ->
            total[ amount: Nat ] ->
            lock.acquire ->
            balance.increase[ amount ] ->
            lock.release ->
            return ->
            DEPOSIT )
+{
    balance.decrease[ amount: Nat ],
    balance.insufficient.funds.exception
}.

TRANSFER = ( call ->
             total[ amount: Nat ] ->
             TRANSFER[ amount ] ),

    TRANSFER[ amount: Nat ] = (
								lock.acquire ->
                                balance.decrease[ amount ] ->
                                
                                ( formal.parameter.account.deposit.call ->
                                  formal.parameter.account.deposit.total[ amount ] ->
                                  formal.parameter.account.deposit.return ->
                                  lock.release ->
                                  return ->
                                  TRANSFER
             
                                | balance.insufficient.funds.exception ->
                                  lock.release ->
                                  return ->
                                  TRANSFER )

								| lock.release ->
								  return -> TRANSFER)
+{
    balance.increase[ amount: Nat ]
}.
                                
||ACCOUNT = ( { withdraw, deposit, transfer }:: lock: LOCK
           || { withdraw, deposit, transfer }:: balance: BALANCE
           || withdraw: WITHDRAW
           || deposit:  DEPOSIT
           || transfer: TRANSFER )
<<{
    { withdraw, transfer }.balance.insufficient.funds.exception
}
\{
    deposit.balance.decrease[ amount: Nat ],
    deposit.balance.insufficient.funds.exception,
    { withdraw, transfer }.balance.increase[ amount: Nat ]
}.
property NoDeadlock = (checking.withdraw.call ->
	checking.withdraw.total ->
	checking.transfer.call ->
	checking.transfer.total ->
	checking.transfer.checkforhold ->
	checking.transfer.lock.acquire ->
	checking.transfer.balance.decrease ->
	savings.deposit.call -> 
	savings.deposit.total ->
	savings.withdraw.call ->
	savings.withdraw.total ->
	savings.transfer.call ->
	savings.transfer.total ->
	savings.transfer.checkforhold ->
	savings.transfer.lock.acquire ->
	savings.transfer.balance.decrease ->
	checking.deposit.call ->
	checking.deposit.total ->
	NoDeadlock
).
//This property will not let the user move money if there is no money to move. When this exception is reached the state is forced to stop
//this can prevent money that does not exist from being transferred on error
property NoImaginaryMoney = ( balance.insufficient.funds.exception -> STOP).
//this progress means that any account object has a trace where it can perform one of the three actions. This keeps accounts from
//being restricted
progress NoRestrictedAccounts = {transfer,withdraw,deposit}
//the lock being aquired always implies that eventually it will be released
assert LockTimeOut = [](lock.aquire->(<>(lock.release))) 
//
assert NoFakeMoney = []!(insufficient.funds.exception->(balance.decrease[ amount: Nat]))

||DEADLOCKSCENARIO = ({ checking, savings }: ACCOUNT )
// Transfer money from a savings account into a checking account
// and from the same checking account to the same savings account
//when two accounts try to transfer at once the situation encounters deadlock
//both will keep trying to transfer to the other but cannot obtain the lock for the other account
/{
    savings.deposit.call                /checking.transfer.formal.parameter.account.deposit.call,
    savings.deposit.total[ amount: Nat ]/checking.transfer.formal.parameter.account.deposit.total[ amount ],
    savings.deposit.return              /checking.transfer.formal.parameter.account.deposit.return,
    
    checking.deposit.call                /savings.transfer.formal.parameter.account.deposit.call,
    checking.deposit.total[ amount: Nat ]/savings.transfer.formal.parameter.account.deposit.total[ amount ],
    checking.deposit.return                /savings.transfer.formal.parameter.account.deposit.return
}.
